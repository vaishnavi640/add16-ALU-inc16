// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=x1);                         
    Not16(in=x1, out=Notx1);
    Mux16(a=x1, b=Notx1, sel=nx, out=x2);                        
    Mux16(a=y, b=false, sel=zy, out=y1);                         
    Not16(in=y1, out=Noty1);
    Mux16(a=y1, b=Noty1, sel=ny, out=y2);                       
    Add16(a=x2, b=y2, out=sum);
    And16(a=x2, b=y2, out=and);
    Mux16(a=and, b=sum, sel=f, out=out1);                         
    Not16(in=out1, out=Notout1);
    Mux16(a=out1, b=Notout1, sel=no,out=out);                     
    Mux16(a=out1, b=Notout1, sel=no,out[0..7]=out0..7,out[8..14]=out8..14,out[15]=out15);
    Or8Way(in=out0..7, out=isOutZeroPart1);
    Or8Way(in[0..6]=out8..14, in[7]=out15, out=isOutZeroPart2);
    Or(a=isOutZeroPart1, b= isOutZeroPart2,out=isOutZero);
    Mux(a=true, b=false, sel=isOutZero, out=zr);                
    Or(a=out15, b=false, out=ng);     
}